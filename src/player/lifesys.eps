import location as loc;
import bulletBase as bBase;


function createPlayer(player) {
    const regenX, regenY = loc.playerRegen(player);
    loc.moveLocationPx($L("pRegen"), regenX, regenY);
    const ptr, epd = bBase.getNextGeneratedUnit();
    if(ptr != 0) {
        CreateUnit(1, "Avoider", "pRegen", player);
        bBase.makeUnitStackable(epd);
    }
}

function createPlayers() {
    for(var player = $P1 ; player <= $P6; player++) {
        if(playerexist(player)) {
            createPlayer(player);
        }
    }
}

/**
 * Regenerate player, if player has sufficient life
 */
function regeneratePlayer(player) {
    // Regenerate player
    if(!Command(player, AtLeast, 1, "Avoider")) {
        if(Accumulate(player, AtLeast, 1, Ore)) {
            createPlayer(player);
            SetCurrentPlayer(player);
            DisplayText("\x05  Retry...");
        }
    }
}


function setLife(player, life) {
    SetResources(player, SetTo, life, Ore);
}


function lifeProc() {
    // 플레이어 부활
    for(var player = $P1 ; player <= $P6; player++) {
        if(playerexist(player)) {
            regeneratePlayer(player);
        }
    }

    // 패배 조건
    if(Accumulate(Force1, Exactly, 0, Ore)) {
        DisplayText(SCMD2Text("\
<04>Game over\n\
<05>  - Defeat();"));
        Defeat();
    }
}
