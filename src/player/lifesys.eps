import location as loc;
import main;

function createPlayer(player) {
    SetResources(player, Subtract, 1, Ore);
    const regenX, regenY = loc.playerRegen(player);
    loc.moveLocationPx($L("pRegen"), regenX, regenY);
    CreateUnit(1, "Avoider", "pRegen", player);    
}

function createPlayers() {
    for(var player = $P1 ; player <= $P6; player++) {
        if(playerexist(player)) {
            createPlayer(player);
        }
    }
}

/**
 * Regenerate player, if player has sufficient life
 */
function regeneratePlayer(player) {
    // Regenerate player
    if(!Command(player, AtLeast, 1, "Avoider")) {
        if(Accumulate(player, AtLeast, 1, Ore)) {
            createPlayer(player);
            SetCurrentPlayer(player);
            DisplayText("\x05  Retry...");
        }
    }
}


function isPlayerAlive(player) {
    return l2v(Command(player, AtLeast, 1, "Avoider"));
}


function addLife(player, life) {
    SetResources(player, Add, life, Ore);
}


function lifeProc() {
    // 플레이어 부활
    for(var player = $P1 ; player <= $P6; player++) {
        if(playerexist(player)) {
            regeneratePlayer(player);
        }
    }

    // 패배 조건
    if(
        Bring(Force1, Exactly, 0, 'Avoider', 'Anywhere') &&
        Accumulate(Force1, Exactly, 0, Ore)
    ) {
        DisplayText(SCMD2Text("\
<04>Game over\
<05>  - Defeat();"));
        Defeat();
        main.issueGameComplete();
    }
}
