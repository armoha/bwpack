import player.playerProc as pProc;
import player.lifesys as lSys;

import bulletBase as bBase;
import bulletTools as bTool;

import unitloop as uLoop;
import stages.stagesys as sSys;

import bgmplayer;


function setBulletFlingy(flingyID) {
    dwwrite(0x6C9930 + 4 * flingyID, 1);
}

function onPluginStart() {
    // 마린 외형/버튼셋
    SetMemory(0x6644F8, SetTo, 0x4B584A0F);  // 마린 그래픽을 0F(저글링)으로
    SetMemory(0x51783C, SetTo, 0x00E90009);  // 버튼셋 그래픽 변경
    SetMemory(0x6616E0, Add, 130);  // 마린의 지상무기 삭제
    SetMemory(0x6636B8, Add, 130);  // 마린의 공중무기 삭제

    setBulletFlingy(43);  // 스카웃 속도 조절

    // 라이프 시스템 초기화
    lSys.setLife($Force1, 20);
    LeaderBoardResources(Ore, "\x04Remaining life");
    LeaderBoardComputerPlayers(Disable);

    // 스테이지 관련
    SetCurrentPlayer(getuserplayerid());
    DisplayText("\x1F♬\x04 cycerin - Spacefighter\n\n");
}

var x = 0;
var timer = 0;
var gameEnded = 0;

function issueGameComplete() {
    gameEnded = 1;
}

function afterTriggerExec() {
    bgmplayer.bgmloop();

    if(gameEnded) return;
    else if(Accumulate(Force1, Exactly, 0, Ore)) {
        issueGameComplete();
        return;
    }

    sSys.runStageProc();
    uLoop.mainUnitLoop();
    lSys.lifeProc();

    SetCurrentPlayer(getuserplayerid());
    MinimapPing('minimapPing');
    SetInvincibility(Enable, '(any unit)', AllPlayers, 'Anywhere');
    ModifyUnitHitPoints(All, '(any unit)', AllPlayers, 'Anywhere', 100);
    SetMemory(0x6D5E1C, SetTo, 1);
}
